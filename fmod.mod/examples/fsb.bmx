'
' This example demonstrates the usage of the FSB format, generated by the FSBank tool.
' It is simply a packed file format, with sounds contained within.  This is a cross platform
' format that is useful to keep the data format optimal for the platform you are targeting.
'
'
SuperStrict

Framework BaH.FMOD
Import BRL.GLMax2d

' Create a System object and initialize.
Local system:TFMODSystem = New TFMODSystem.Create()
system.Init(32)


Local fsb:TFMODSound = system.CreateSoundURL("media/example.fsb", FMOD_DEFAULT)

Local numsubsounds:Int
fsb.GetNumSubSounds(numsubsounds)

Local names:String[] = New String[numsubsounds]

For Local count:Int = 0 Until numsubsounds
	Local subsound:TFMODSound = fsb.GetSubSound(count)
	
	names[count] = subsound.GetName()
Next

Local channel:TFMODChannel

Graphics 640, 480, 0

While Not KeyDown(KEY_ESCAPE)

	Cls
	
		DrawText "===================================================================", 50, 10
		DrawText "FSB Example.", 50, 30
		DrawText "===================================================================", 50, 50
		For Local i:Int = 0 Until names.length
			DrawText "Press '" + (i + 1) + "' to Play ~q" + names[i] + "~q", 50, 70 + i * 20
		Next
		DrawText "Press 'Esc' to quit", 50, 70 + names.length * 20


		For Local i:Int = 0 Until numsubsounds
			If KeyHit(KEY_1 + i) Then
				Local subsound:TFMODSound = fsb.GetSubSound(i)
				channel = system.PlaySound(FMOD_CHANNEL_FREE, subsound)
			End If
		Next
	
		system.Update()
	
		Local playing:Int
		Local paused:Int
		Local pos:Int
		Local lenms:Int
		Local channelsPlaying:Int
		
		If channel Then
			channel.IsPlaying(playing)
			
			channel.GetPaused(paused)
			
			channel.GetPosition(pos, FMOD_TIMEUNIT_MS)
			
			Local currentSound:TFMODSound = channel.GetCurrentSound()			
			If currentSound Then
				currentSound.GetLength(lenms, FMOD_TIMEUNIT_MS)
			End If

		End If
		
		system.GetChannelsPlaying(channelsPlaying)
		
		Local s:String = "Time " + Pad(pos / 1000 / 60) + ":" + Pad(pos / 1000 Mod 60) + ..
			":" + Pad(pos / 10 Mod 100) + " /" + Pad(lenms / 1000 / 60) + ":" + Pad(lenms / 1000 Mod 60) + ..
			":" + Pad(lenms / 10 Mod 100)
			
		If paused Then
			s:+ " : Paused"
		Else If playing Then
			s:+ " : Playing"
		Else
			s:+ " : Stopped"
		End If
		
		s:+ " : Channels Playing : " + channelsPlaying
		
		DrawText s, 50, 200
		
	Flip

Wend

' shut down
fsb.SoundRelease()

system.Close()
system.SystemRelease()


End

Function Pad:String(val:Int, size:Int = 2)
	Local s:String = "00" + val
	Return s[s.length - size..]
End Function

